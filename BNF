<valid scalver> ::= <version core>
                  | <version core> "-" <pre-release>
                  | <version core> "+" <build>
                  | <version core> "-" <pre-release> "+" <build>

<version core>   ::= <major> "." <date> "." <patch>

<major> ::= <numeric identifier>
<patch> ::= <numeric identifier>

<date>  ::= <year>
          | <year> <month>
          | <year> <month> <day>

<year>  ::= <positive digit> <digit> <digit> <digit>
<month> ::= "01" | "02" | "03" | "04" | "05" | "06"
          | "07" | "08" | "09" | "10" | "11" | "12"
<day>   ::= "01" | "02" | "03" | "04" | "05" | "06" | "07" | "08" | "09"
          | "10" | "11" | "12" | "13" | "14" | "15" | "16" | "17" | "18" | "19"
          | "20" | "21" | "22" | "23" | "24" | "25" | "26" | "27" | "28" | "29"
          | "30" | "31"

<pre-release> ::= <dot-separated pre-release identifiers>

<dot-separated pre-release identifiers>
               ::= <pre-release identifier>
               |  <pre-release identifier> "." <dot-separated pre-release identifiers>

<build> ::= <dot-separated build identifiers>

<dot-separated build identifiers>
          ::= <build identifier>
          |  <build identifier> "." <dot-separated build identifiers>

<pre-release identifier> ::= <alphanumeric identifier>
                           |  <numeric identifier>

<build identifier> ::= <alphanumeric identifier>
                     |  <digits>

<alphanumeric identifier> ::= <non-digit>
                            |  <non-digit> <identifier characters>
                            |  <identifier characters> <non-digit>
                            |  <identifier characters> <non-digit> <identifier characters>

<numeric identifier> ::= "0"
                       |  <positive digit>
                       |  <positive digit> <digits>

<identifier characters> ::= <identifier character>
                          |  <identifier character> <identifier characters>

<identifier character> ::= <digit>
                         |  <non-digit>

<non-digit> ::= <letter>
              |  "-"

<digits> ::= <digit>
           |  <digit> <digits>

<digit> ::= "0" | <positive digit>
<positive digit> ::= "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"

<letter> ::= "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J"
           | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T"
           | "U" | "V" | "W" | "X" | "Y" | "Z"
           | "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j"
           | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t"
           | "u" | "v" | "w" | "x" | "y" | "z"

